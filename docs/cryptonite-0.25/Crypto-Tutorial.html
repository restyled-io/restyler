<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd"><html xmlns="http://www.w3.org/1999/xhtml"><head><meta http-equiv="Content-Type" content="text/html; charset=UTF-8" /><title>Crypto.Tutorial</title><link href="ocean.css" rel="stylesheet" type="text/css" title="Ocean" /><script src="haddock-util.js" type="text/javascript"></script><script src="https://cdnjs.cloudflare.com/ajax/libs/mathjax/2.7.0/MathJax.js?config=TeX-AMS-MML_HTMLorMML" type="text/javascript"></script><script type="text/javascript">//<![CDATA[
window.onload = function () {pageLoad();};
//]]>
</script></head><body><div id="package-header"><ul class="links" id="page-menu"><li><a href="src/Crypto.Tutorial.html">Source</a></li><li><a href="index.html">Contents</a></li><li><a href="doc-index.html">Index</a></li></ul><p class="caption">cryptonite-0.25: Cryptography Primitives sink</p></div><div id="content"><div id="module-header"><table class="info"><tr><th>Safe Haskell</th><td>Safe</td></tr><tr><th>Language</th><td>Haskell2010</td></tr></table><p class="caption">Crypto.Tutorial</p></div><div id="table-of-contents"><p class="caption">Contents</p><ul><li><a href="#g:1">API design</a></li><li><a href="#g:2">Hash algorithms</a></li><li><a href="#g:3">Symmetric block ciphers</a></li></ul></div><div id="description"><p class="caption">Description</p><div class="doc"><p>Examples of how to use <code>cryptonite</code>.</p></div></div><div id="synopsis"><p id="control.syn" class="caption expander" onclick="toggleSection('syn')">Synopsis</p><ul id="section.syn" class="hide" onclick="toggleSection('syn')"></ul></div><div id="interface"><h1 id="g:1">API design</h1><div class="doc"><p>APIs in cryptonite are often based on type classes from package
 <a href="https://hackage.haskell.org/package/memory">memory</a>, notably
 <code><a href="../memory-0.14.16/Data-ByteArray.html#v:ByteArrayAccess">ByteArrayAccess</a></code> and <code><a href="../memory-0.14.16/Data-ByteArray.html#v:ByteArray">ByteArray</a></code>.
 Module <a href="../memory-0.14.16/Data-ByteArray.html">Data.ByteArray</a> provides many primitives that are useful to
 work with cryptonite types.  For example function <code><a href="../memory-0.14.16/Data-ByteArray.html#v:convert">convert</a></code>
 can transform one <code><a href="../memory-0.14.16/Data-ByteArray.html#v:ByteArrayAccess">ByteArrayAccess</a></code> concrete type like
 <code><a href="Crypto-Hash.html#v:Digest">Digest</a></code> to a <code><a href="../bytestring-0.10.8.2/Data-ByteString.html#v:ByteString">ByteString</a></code>.</p><p>Algorithms and functions needing random bytes are based on type class
 <code><a href="Crypto-Random-Types.html#v:MonadRandom">MonadRandom</a></code>.  Implementation <code><a href="../base-4.10.1.0/System-IO.html#t:IO">IO</a></code> uses a system source
 of entropy.  It is also possible to use a <code><a href="Crypto-Random-Types.html#v:DRG">DRG</a></code> with
 <code><a href="Crypto-Random-Types.html#v:MonadPseudoRandom">MonadPseudoRandom</a></code></p><p>Error conditions are returned with data type <code><a href="Crypto-Error.html#v:CryptoFailable">CryptoFailable</a></code>.
 Functions in module <a href="Crypto-Error.html">Crypto.Error</a> can convert those values to runtime
 exceptions, <code><a href="../base-4.10.1.0/Data-Maybe.html#t:Maybe">Maybe</a></code> or <code><a href="../base-4.10.1.0/Data-Either.html#t:Either">Either</a></code> values.</p></div><h1 id="g:2">Hash algorithms</h1><div class="doc"><p>Hashing a complete message:</p><pre>import Crypto.Hash

import Data.ByteString (ByteString)

exampleHashWith :: ByteString -&gt; IO ()
exampleHashWith msg = do
    putStrLn $ &quot;  sha1(&quot; ++ show msg ++ &quot;) = &quot; ++ show (hashWith SHA1   msg)
    putStrLn $ &quot;sha256(&quot; ++ show msg ++ &quot;) = &quot; ++ show (hashWith SHA256 msg)</pre><p>Hashing incrementally, with intermediate context allocations:</p><pre>{-# LANGUAGE OverloadedStrings #-}

import Crypto.Hash

import Data.ByteString (ByteString)

exampleIncrWithAllocs :: IO ()
exampleIncrWithAllocs = do
    let ctx0 = hashInitWith SHA3_512
        ctx1 = hashUpdate ctx0 (&quot;The &quot;   :: ByteString)
        ctx2 = hashUpdate ctx1 (&quot;quick &quot; :: ByteString)
        ctx3 = hashUpdate ctx2 (&quot;brown &quot; :: ByteString)
        ctx4 = hashUpdate ctx3 (&quot;fox &quot;   :: ByteString)
        ctx5 = hashUpdate ctx4 (&quot;jumps &quot; :: ByteString)
        ctx6 = hashUpdate ctx5 (&quot;over &quot;  :: ByteString)
        ctx7 = hashUpdate ctx6 (&quot;the &quot;   :: ByteString)
        ctx8 = hashUpdate ctx7 (&quot;lazy &quot;  :: ByteString)
        ctx9 = hashUpdate ctx8 (&quot;dog&quot;    :: ByteString)
    print (hashFinalize ctx9)</pre><p>Hashing incrementally, updating context in place:</p><pre>{-# LANGUAGE OverloadedStrings #-}

import Crypto.Hash.Algorithms
import Crypto.Hash.IO

import Data.ByteString (ByteString)

exampleIncrInPlace :: IO ()
exampleIncrInPlace = do
    ctx &lt;- hashMutableInitWith SHA3_512
    hashMutableUpdate ctx (&quot;The &quot;   :: ByteString)
    hashMutableUpdate ctx (&quot;quick &quot; :: ByteString)
    hashMutableUpdate ctx (&quot;brown &quot; :: ByteString)
    hashMutableUpdate ctx (&quot;fox &quot;   :: ByteString)
    hashMutableUpdate ctx (&quot;jumps &quot; :: ByteString)
    hashMutableUpdate ctx (&quot;over &quot;  :: ByteString)
    hashMutableUpdate ctx (&quot;the &quot;   :: ByteString)
    hashMutableUpdate ctx (&quot;lazy &quot;  :: ByteString)
    hashMutableUpdate ctx (&quot;dog&quot;    :: ByteString)
    hashMutableFinalize ctx &gt;&gt;= print</pre></div><h1 id="g:3">Symmetric block ciphers</h1><div class="doc"><pre>{-# LANGUAGE OverloadedStrings #-}
{-# LANGUAGE ScopedTypeVariables #-}
{-# LANGUAGE GADTs #-}

import           Crypto.Cipher.AES (AES256)
import           Crypto.Cipher.Types (BlockCipher(..), Cipher(..), nullIV, KeySizeSpecifier(..), IV, makeIV)
import           Crypto.Error (CryptoFailable(..), CryptoError(..))

import qualified Crypto.Random.Types as CRT

import           Data.ByteArray (ByteArray)
import           Data.ByteString (ByteString)

-- | Not required, but most general implementation
data Key c a where
  Key :: (BlockCipher c, ByteArray a) =&gt; a -&gt; Key c a

-- | Generates a string of bytes (key) of a specific length for a given block cipher
genSecretKey :: forall m c a. (CRT.MonadRandom m, BlockCipher c, ByteArray a) =&gt; c -&gt; Int -&gt; m (Key c a)
genSecretKey _ = fmap Key . CRT.getRandomBytes

-- | Generate a random initialization vector for a given block cipher
genRandomIV :: forall m c. (CRT.MonadRandom m, BlockCipher c) =&gt; c -&gt; m (Maybe (IV c))
genRandomIV _ = do
  bytes :: ByteString &lt;- CRT.getRandomBytes $ blockSize (undefined :: c)
  return $ makeIV bytes

-- | Initialize a block cipher
initCipher :: (BlockCipher c, ByteArray a) =&gt; Key c a -&gt; Either CryptoError c
initCipher (Key k) = case cipherInit k of
  CryptoFailed e -&gt; Left e
  CryptoPassed a -&gt; Right a

encrypt :: (BlockCipher c, ByteArray a) =&gt; Key c a -&gt; IV c -&gt; a -&gt; Either CryptoError a
encrypt secretKey initIV msg =
  case initCipher secretKey of
    Left e -&gt; Left e
    Right c -&gt; Right $ ctrCombine c initIV msg

decrypt :: (BlockCipher c, ByteArray a) =&gt; Key c a -&gt; IV c -&gt; a -&gt; Either CryptoError a
decrypt = encrypt

exampleAES256 :: ByteString -&gt; IO ()
exampleAES256 msg = do
  -- secret key needs 256 bits (32 * 8)
  secretKey &lt;- genSecretKey (undefined :: AES256) 32
  mInitIV &lt;- genRandomIV (undefined :: AES256)
  case mInitIV of
    Nothing -&gt; error &quot;Failed to generate and initialization vector.&quot;
    Just initIV -&gt; do
      let encryptedMsg = encrypt secretKey initIV msg
          decryptedMsg = decrypt secretKey initIV =&lt;&lt; encryptedMsg
      case (,) &lt;$&gt; encryptedMsg &lt;*&gt; decryptedMsg of
        Left err -&gt; error $ show err
        Right (eMsg, dMsg) -&gt; do
          putStrLn $ &quot;Original Message: &quot; ++ show msg
          putStrLn $ &quot;Message after encryption: &quot; ++ show eMsg
          putStrLn $ &quot;Message after decryption: &quot; ++ show dMsg</pre></div></div></div><div id="footer"><p>Produced by <a href="http://www.haskell.org/haddock/">Haddock</a> version 2.18.1</p></div></body></html>